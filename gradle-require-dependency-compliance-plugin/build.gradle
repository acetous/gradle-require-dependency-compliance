plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'org.sonarqube' version '2.6.1'
    id 'jacoco'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.3'
}

// read all scripts from ./gradle.d
file('gradle.d').listFiles().sort().each {
    if (it =~ /.*\.gradle$/) {
        apply from: relativePath(it)
    }
}

ext {
    // must be  "major.minor.patch"
    // or       "major.minor.patch-prerelease"
    // see http://semver.org/
    baseVersion = '1.3.1-rc'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'de.acetous'
version = getBuildVersion(baseVersion)

gradlePlugin {
    plugins {
        dependencyCompliance {
            id = 'de.acetous.dependency-compliance'
            implementationClass = 'de.acetous.dependencycompliance.DependencyCompliancePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/acetous/gradle-require-dependency-compliance'
    vcsUrl = 'https://github.com/acetous/gradle-require-dependency-compliance.git'

    plugins {
        dependencyCompliance {
            id = 'de.acetous.dependency-compliance'
            displayName = 'Gradle Dependency Compliance Plugin'
            description = 'A Gradle plugin to check and track dependencies in your project. Useful for meeting compliance targets, including 3rd-party libraries.'
            tags = ['dependencies', 'compliance']
            version = getReleaseVersion(baseVersion)
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile(
            'com.google.code.gson:gson:2.8.5',
    )
    testCompile(
            'junit:junit:4.12',
            'commons-io:commons-io:2.4',
            'org.assertj:assertj-core:3.10.0',
            'com.google.guava:guava:25.1-jre',
    )
}
